module ALU_6 interface ([Op4..Op0], [A3..A0], [B3..B0] -> [C3..C0], ZF, CF)

declarations

	@include 'opcodes.abl'

	Op4..Op0 pin;

	A3..A0 pin;
	B3..B0 pin;
	C3..C0 pin;

	ZF	pin;
	CF	pin;

	opcode 	= [Op4..Op0];
	A	= [A3..A0];
	B	= [B3..B0];
	C	= [C3..C0];

equations

	when (opcode == OP_AND) then C = A & B;

	when (opcode == OP_OR ) then C = A # B;

	when (opcode == OP_XOR) then C = A $ B;

	when (opcode == OP_NOT) then C = !A;

	when (opcode == OP_INCA) then [CF,C3..C0] = [0,A3..A0] + 1;

	when (opcode == OP_INCB) then [CF,C3..C0] = [0,B3..B0] + 1;

	when (opcode == OP_DECA) then { C = A - 1; CF = (A == 0); }

	when (opcode == OP_DECB) then { C = B - 1; CF = (B == 0); }

	when (opcode == OP_SHL) then [CF,C3..C0] = [A3,A2,A1,A0,0];

	when (opcode == OP_SHR) then [C3,C2,C1,C0,CF] = [0,A3,A2,A1,A0];


	ZF = (C == 0);
	
end 
